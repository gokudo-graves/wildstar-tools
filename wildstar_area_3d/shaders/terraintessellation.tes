#version 400

layout( quads, fractional_even_spacing, cw ) in;

in terrainVertex {
    vec2 position;
    int chunk;
} In[];

out worldVertex {
    vec4 worldPosition;
    vec3 worldNormal;
    vec4 position;
    vec3 normal;
    vec2 texCoords;
} Out;

uniform usampler2DArray heightMap;

// The number of triangles created per height-map texel
uniform int maxTrianglesPerTexel = 10;

// Distance between each tessellation point at max tess level
uniform float horizontalScale = 10.0;

// Vertical scale to multiply height samples by
uniform float verticalScale = 1.0; //1.0 / 8.0;

// Transformation matrices
uniform mat4 modelMatrix;
uniform mat4 modelViewMatrix;
uniform mat3 worldNormalMatrix;
uniform mat3 normalMatrix;
uniform mat4 mvp;

const float maxTessLevel = 18.0;

void main()
{
    const float scale_horizintal = 2.0;
    // Calculate extent of this patch in texture coords [0,1]
    vec2 patchExtent = maxTessLevel / ( textureSize( heightMap, 0 ).xy );

    // Calculate the texture coordinates
    Out.texCoords = In[0].position.xy + gl_TessCoord.xy * patchExtent;
    vec3 tex_coords;
    tex_coords.xy = Out.texCoords;
    tex_coords.z = 0;

    // Calculate the model-space position
    vec4 position;
    position.xz = Out.texCoords * scale_horizintal;
    position.y = verticalScale * texture( heightMap, tex_coords ).r;
    position.w = 1.0;

    // Transform the position to world coordinates and to eye space
    Out.worldPosition = modelMatrix * position;
    Out.position = modelViewMatrix * position;

    // Calculate the normal
    // This could be moved to a one-time pre-process step to create a normal map.
    // This would be a good candidate for a compute shader.
    // For deformable terrain, would need re-generating when terrain is modified
    const ivec3 offset = ivec3( -1, 0, 1 ); // Texel offsets
    float delta = 2.0 * scale_horizintal / textureSize( heightMap, 0 ).x; // Horizontal displacement in world coords
    float left   = verticalScale * textureOffset( heightMap, tex_coords, offset.xy ).r;
    float right  = verticalScale * textureOffset( heightMap, tex_coords, offset.zy ).r;
    float top    = verticalScale * textureOffset( heightMap, tex_coords, offset.yz ).r;
    float bottom = verticalScale * textureOffset( heightMap, tex_coords, offset.yx ).r;

    vec3 x = normalize( vec3( delta, right - left, 0.0 ) );
    vec3 z = normalize( vec3( 0.0, top - bottom, delta ) );
    vec3 n = cross( z, x );
    Out.worldNormal = worldNormalMatrix * n;
    Out.normal = normalMatrix * n;

    // Transform to clip-space
    gl_Position = mvp * position;
}
